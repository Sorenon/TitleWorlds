plugins {
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
}

subprojects {
    apply plugin: "dev.architectury.loom"
    if (project.path != ':common') {

        loom {
            runConfigs.configureEach { ideConfigGenerated = true }
            mods {
                main { // to match the default mod generated for Forge
                    sourceSet project.sourceSets.main
                    sourceSet project(':common').sourceSets.main
                }
            }
        }
    }
}

subprojects {
    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.19.2+build.3:v2"

        include(implementation("com.electronwill.night-config:core:${rootProject.properties["night_config_version"].toString()}"))
        include(implementation("com.electronwill.night-config:toml:${rootProject.properties["night_config_version"].toString()}"))
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven {
            name = "Modrinth"
            url = uri("https://api.modrinth.com/maven")
            content {
                includeGroup("maven.modrinth")
            }
        }
        maven {
            name = "shedaniel"
            url = uri("https://maven.shedaniel.me/")
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
